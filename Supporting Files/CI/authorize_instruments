#!/usr/bin/osascript

(*
For details and documentation:
http://github.com/inkling/Subliminal

Copyright 2013 Inkling Systems, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*)

(*
Waits for an authorization dialog to appear when instruments is launched, 
then dismisses it by entering the specified login password.

This is a workaround for http://openradar.appspot.com/radar?id=1544403.

This script is expected to be put into the background right before 
instruments is launched:

	osascript authorize_instruments "$LOGIN_PASSWORD" &
	# Kill the process when the parent script ends
	trap "kill $!" SIGINT SIGTERM EXIT

	xcrun instruments ...

*)

on run argv
	set loginPassword to item 1 of argv
	
	-- begin with a short delay, to catch the prompt under the assumption 
	-- that instruments will be launched immediately after this is run
	delay 4.0
	tell application "System Events"
		repeat while "SecurityAgent" is not in name of processes
			-- long delay: it is likely that the prompt will not appear this run
			-- and we don't want to impede instruments' performance
			delay 10.0
		end repeat
		tell process "SecurityAgent"
			set value of text field 2 of scroll area 1 of group 1 of window 1 to loginPassword
			click button "Take Control" of group 2 of window 1
			return	-- just so the script doesn't print the last command executed
		end tell
	end tell
end run
