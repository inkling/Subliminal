#!/usr/bin/osascript

(*
For details and documentation:
http://github.com/inkling/Subliminal

Copyright 2013 Inkling Systems, Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*)

(*
Sets the simulator's device to the specified type.

Acceptable types are listed in the iPhone Simulator's "Hardware -> Device" menu.
*)

on run argv
	-- Ensure GUI scripting is enabled
	tell application "Finder"
		set scriptDir to container of (path to me) as text
	end tell
	
	set enableGUIScriptingScript to (scriptDir & "enable_GUI_scripting")
	set GUIScriptingEnabled to (run script (enableGUIScriptingScript as alias))
	if not GUIScriptingEnabled then return false
	
	-- Select simulator device type
	set deviceType to item 1 of argv
	tell application "iPhone Simulator"
		activate
	end tell
	
	tell application "System Events"
		tell process "iPhone Simulator"
			tell menu bar 1
				tell menu "Hardware" of menu bar item "Hardware"
					tell menu "Device" of menu item "Device"
						click menu item deviceType
					end tell
				end tell
			end tell
		end tell
	end tell
	
	delay 0.1
	return true
end run
